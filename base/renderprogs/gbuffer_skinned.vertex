/*
===========================================================================
G-Buffer skinned vertex shader - outputs VIEW-SPACE normals.
Skin transform first, then rpModelViewMatrixX/Y/Z for view space.
===========================================================================
*/

#include "global.inc"

uniform matrices_ubo { float4 matrices[408]; };

struct VS_IN {
	float4 position : POSITION;
	float4 normal   : NORMAL;
	float4 color    : COLOR0;
	float4 color2   : COLOR1;
};

struct VS_OUT {
	float4 position   : POSITION;
	float3 texcoord0  : TEXCOORD0;
};

void main( VS_IN vertex, out VS_OUT result ) {
	const float w0 = vertex.color2.x;
	const float w1 = vertex.color2.y;
	const float w2 = vertex.color2.z;
	const float w3 = vertex.color2.w;

	float4 matX, matY, matZ;
	float joint = vertex.color.x * 255.1 * 3;
	matX = matrices[int(joint+0)] * w0;
	matY = matrices[int(joint+1)] * w0;
	matZ = matrices[int(joint+2)] * w0;

	joint = vertex.color.y * 255.1 * 3;
	matX += matrices[int(joint+0)] * w1;
	matY += matrices[int(joint+1)] * w1;
	matZ += matrices[int(joint+2)] * w1;

	joint = vertex.color.z * 255.1 * 3;
	matX += matrices[int(joint+0)] * w2;
	matY += matrices[int(joint+1)] * w2;
	matZ += matrices[int(joint+2)] * w2;

	joint = vertex.color.w * 255.1 * 3;
	matX += matrices[int(joint+0)] * w3;
	matY += matrices[int(joint+1)] * w3;
	matZ += matrices[int(joint+2)] * w3;

	float4 modelPosition;
	modelPosition.x = dot4( matX, vertex.position );
	modelPosition.y = dot4( matY, vertex.position );
	modelPosition.z = dot4( matZ, vertex.position );
	modelPosition.w = 1.0;

	result.position.x = dot4( modelPosition, rpMVPmatrixX );
	result.position.y = dot4( modelPosition, rpMVPmatrixY );
	result.position.z = dot4( modelPosition, rpMVPmatrixZ );
	result.position.w = dot4( modelPosition, rpMVPmatrixW );

	float3 localNormal = vertex.normal.xyz * 2.0 - 1.0;
	float3 skinnedNormal;
	skinnedNormal.x = dot3( matX, localNormal );
	skinnedNormal.y = dot3( matY, localNormal );
	skinnedNormal.z = dot3( matZ, localNormal );

	float3 viewN;
	viewN.x = dot3( skinnedNormal, rpModelViewMatrixX );
	viewN.y = dot3( skinnedNormal, rpModelViewMatrixY );
	viewN.z = dot3( skinnedNormal, rpModelViewMatrixZ );

	result.texcoord0 = viewN;
}
